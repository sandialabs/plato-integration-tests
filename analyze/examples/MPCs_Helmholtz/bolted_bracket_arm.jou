reset 
version

## Parameters:
#{base_width=2.5}
#{base_height=3.0}
#{base_thickness=0.5}
#{bracket_length=4.0}
#{bracket_thickness=0.5}
#{vert_fillet=0.25}
#{base_fillet=0.375}
#{end_fillet=0.5}
#{face_fillet=0.1}
#{bolt_radius=0.125}
#{bolt_inset=0.375}
#{mount_radius=0.5}
#{mount_inset=0.5}
#{mesh_size=0.5}
#{arm_length=5.0}

# create bracket
create brick x {base_thickness} y {base_height} z {base_width}
create brick x {bracket_length} y {base_height} z {bracket_thickness}
move surface 10 location surface 6
unite volume all
tweak curve 25 26 fillet radius {vert_fillet}
tweak curve 2 4 8 6 fillet radius {base_fillet}
tweak curve 24 22 fillet radius {end_fillet}
tweak curve 20 60 17 55 18 33 41 27 5 44 28 31 fillet radius {face_fillet}
tweak curve 49 29 37 1 45 30 35 16 57 13 59 14 fillet radius {face_fillet}
create cylinder height 1 radius {bolt_radius}
rotate volume 3 about y angle 90
move volume 3 y {base_height/2.0-bolt_inset} z {base_width/2.0-bolt_inset}
volume 3 copy
subtract volume 4 from volume 1
move volume 3 y {-base_height+2.0*bolt_inset}
volume 3 copy
subtract volume 5 from volume 1
move volume 3 z {-base_width+2.0*bolt_inset}
volume 3 copy
subtract volume 6 from volume 1
move volume 3 y { base_height-2.0*bolt_inset}
volume 3 copy
subtract volume 7 from volume 1
delete volume 3

# create bolts
create cylinder height 1 radius {2.0*bolt_radius}
rotate volume 8 about y angle 90
move volume 8 y {base_height/2.0-bolt_inset} z {base_width/2.0-bolt_inset}
volume 8 copy
webcut volume all with tool volume 9
delete volume 8
move volume 9 y {-base_height+2.0*bolt_inset}
webcut volume all with tool volume 9
move volume 9 z {-base_width+2.0*bolt_inset}
webcut volume all with tool volume 9
move volume 9 y { base_height-2.0*bolt_inset}
webcut volume all with tool volume 9
delete volume 9

# create arm
create cylinder height {arm_length} radius {mount_radius}
move volume 14 x {base_thickness/2.0+bracket_length-mount_radius-mount_inset}
webcut vol 1 with tool vol 14
delete vol 15

# webcut for meshing
webcut vol 1 16 with plane yplane
webcut vol 1 16 22 23 with plane zplane

# imprint for side sets
imprint vol 1 16 17 18 14

# merge parts
merge vol 1 16 17 18

# mesh
volume all scheme tetmesh
volume all size {mesh_size}
vol 14 size {mesh_size*2}
mesh volume all

# assign blocks
block 1 vol 1 16 17 18
block 1 name "block_1"
block 2 volume 10 11 12 13
block 2 name "block_2"
block 3 volume 14
block 3 name "block_3"

# assign node and side sets
nodeset 1 surface 53 71 65 59
nodeset 1 name "ns_1"

nodeset 2 surface 83 89 95 101
nodeset 2 name "child_1"

nodeset 3 surface 180 182 183 184 # arm surface
nodeset 3 name "child_2"

sideset 1 surf 105 106
sideset 1 name "ss_1"

# export
export mesh "bolted_bracket_arm.exo" overwrite










